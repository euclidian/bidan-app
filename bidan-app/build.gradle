description = 'bidan-app - Application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:1.2.3"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.1.0'
        classpath 'org.apache.commons:commons-lang3:3.3.2'
        classpath 'org.robolectric:robolectric-gradle-plugin:1.1.0'
    }
}

apply plugin: "jacoco"
apply plugin: 'com.android.application'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'org.robolectric'

android {

    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    signingConfigs {
        config {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("$rootDir/assets/security/debug.keystore")
            storePassword 'android'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled = true
            signingConfig signingConfigs.config
            zipAlignEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        // multiDexEnabled = true
        applicationId "org.ei.bidan"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDir 'src/main/java'
            res.srcDirs = ['res']
            assets.srcDir 'assets'
            jniLibs.srcDirs 'src/main/native'
            resources.srcDir 'resources'
        }
        androidTest.setRoot('src/androidTest')
    }

    productFlavors {
        arm {
            ndk {
                abiFilters "armeabi-v7a", "armeabi"
            }
        }
    }

    aaptOptions {
        noCompress 'apk'
    }

    // avoid errors with message 'Duplicate files copied in APK ...'
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

task createJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

def filesToCopy = copySpec{
    from "${projectDir}/src/test/resources"
}

task copyTestResources() {
    def path = "${buildDir}/intermediates/classes/test/arm"
    def paths = [ "${path}/debug", "${path}/release"]
    paths.each { dest ->
        copy {
            with filesToCopy
            into dest
        }
    }
}

assembleDebug.dependsOn(copyTestResources)

configurations.all {
    resolutionStrategy.force 'commons-codec:commons-codec:1.6'
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'commons-io:commons-io:2.1'
    compile 'com.google.code.gson:gson:2.3'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'com.google.guava:guava:16.0'
    compile 'info.guardianproject:sqlcipher:2.2.0'
    compile 'org.ei.drishti:drishti-interface:0.1-SNAPSHOT'
    compile 'org.mozilla:rhino:1.7R4'
    compile 'com.ocpsoft:ocpsoft-pretty-time:1.0.7'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // CardLib
    // Core
    compile 'com.github.gabrielemariotti.cards:cardslib-core:2.1.0'
    //CardView
    compile 'com.android.support:cardview-v7:22.1.1'
    // Google Play Service
    compile 'com.google.android.gms:play-services-base:6.5.87'
}

dependencies {
    // Espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:4.3'

    testCompile group: 'com.google.android', name: 'android-test', version:'4.1.1.4'
    testCompile 'junit:junit:4.11'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile('com.squareup:fest-android:1.0.+') { exclude module: 'support-v4' }
    testCompile 'org.robolectric:robolectric:2.4'
}

jacoco {
    toolVersion = "0.7.0.201403182114"
}

import org.apache.commons.lang3.StringUtils
gradle.projectsEvaluated {
    def variants = android.applicationVariants.collect()

    tasks.withType(Test) { task ->
        try {
            variants.each { variant ->
                def buildTypeName = variant.buildType.name.capitalize()

                def productFlavorNames = variant.productFlavors.collect { it.name.capitalize() }
                if (productFlavorNames.isEmpty()) {
                    productFlavorNames = [""]
                }
                def productFlavorName = productFlavorNames.join('')

                def variationName = "${productFlavorName}${buildTypeName}"

                if (task.name.contains(variationName)) {
                    def variationPath = variant.buildType.name;

                    if (StringUtils.isNotEmpty(productFlavorName)) {
                        variationPath = StringUtils.uncapitalize(productFlavorName) + "/" + variationPath
                    }

                    def copyResourcesTask = project.tasks.create("copy${variationName}Resources", Copy)
                    copyResourcesTask.from("${projectDir}/src/test/resources")
                    copyResourcesTask.into("${buildDir}/intermediates/classes/test/${variationPath}")

                    task.dependsOn(copyResourcesTask)

                    variants.remove(variant)

                    throw new Exception("Break") // Breaking the loop
                }
            }
        } catch (Exception e) {} // Just drop the exception
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['test']) {
    description = "Generates Jacoco coverage reports: XML and HTML"
    group = "Reporting"

    // use hidden configuration, for details look into JacocoPlugin.groovy
    jacocoClasspath = project.configurations['androidJacocoAnt']

    // exclude auto-generated classes and tests
    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*',
                      'android/**/*.*']
    def debugTree = fileTree(dir: "${project.buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.projectDir, includes: ['**/*.exec', '**/*.ec'])

    reports {
        xml {
            enabled = true
            destination = "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        }
        csv.enabled false
        html {
            enabled = true
            destination = "${project.buildDir}/reports/jacoco"
        }
    }
}